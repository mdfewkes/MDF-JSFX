desc:Envelope to CC
author: Michael Fewkes
version: 0.9
changelog: Initial release
about: Convert audio to CC

slider1:10<1,100,1>Follower Responsiveness In Hz
slider2:2<1,127,1>CC Number
slider3:1<1,16,1>MIDI Channel
slider4:1<-10,10,0.01>Scale CC

@init
/////////////////////////////////////////////////////////////////
// Copyright (c) - 2014 - Olivier Belanger                     //
// Permission to use, copy, modify, and/or distribute this     //
// software for any purpose with or without fee is hereby      //
// granted, provided that the above copyright notice and this  //
// permission notice appear in all copies. CookDSP IS PROVIDED //
// "AS IS", WITHOUT WARRANTY OF ANY KIND FROM THE AUTHOR.      //
/////////////////////////////////////////////////////////////////
function lop(freq)
(
  this.y0 = 0;
  this.ny = srate * 0.49;
  freq < 0.1 ? freq = 0.1 : freq > this.ny ? freq = this.ny;
  this.freq = freq; 
  this.c = exp(-2 * $pi * this.freq / srate);
);

function lop_set_freq(freq)
(
  freq < 0.1 ? freq = 0.1 : freq > this.ny ? freq = this.ny;
  this.freq = freq; 
  this.c = exp(-2 * $pi * this.freq / srate);
);

function lop_do(signal)
(
  this.y0 = signal + (this.y0 - signal) * this.c;
  this.y0;
);

function follow(freq)
(
    this.lp.lop(freq);
    this.freq = this.lp.freq; 
);

function follow_set_freq(freq)
(
    this.lp.lop_set_freq(freq);
    this.freq = this.lp.freq; 
);

function follow_do(signal)
(
    this.lp.lop_do(abs(signal));
);


fol.follow(slider1);
amp = 0;
newLevel = 0;
level = 0;
ccNumber = 2;
midiChannel = 0;
scale = 1;

@slider
fol.follow_set_freq(slider1);
ccNumber = slider2;
midiChannel = slider3 - 1;
scale = slider4;

@block
while (midirecv(offset,msg1,msg2,msg3)) ( // REAPER 4.59+ syntax while()

       midisend(offset,msg1,msg2,msg3);
    );
newLevel = floor(amp * 127 * scale);
newLevel > 127 ? newLevel = 127;
newLevel != level ? (
	level = newLevel;
	 midisend(0, $xB0 + midiChannel, ccNumber, level);
)


@sample
amp = fol.follow_do((spl0+spl1)*0.7);
spl0 = 0;
spl1 = 0;
